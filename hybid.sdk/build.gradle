//==================================================================================================
// PROJECT
//==================================================================================================
def project_name = project.name
def version_name = project.version
if (branchName != "master") {
    version_name = "${version_name}-${branchName}.${buildNumber}"
    project_name = "${project_name}.beta"
}
version = version_name

//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode System.getenv("CIRCLE_BUILD_NUM") ? System.getenv("CIRCLE_BUILD_NUM").toInteger() : 1
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled System.getenv("CI") ? true : false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'lite.sdk.pro'
            buildConfigField "String", "OMIDPV", "\"$omid_version\""
            buildConfigField "String", "OMIDPN", "\"pubnativenet\""
            buildConfigField "String", "SDK_VERSION", "\"$version_name\""
            buildConfigField "String", "BASE_URL", "\"https://api.pubnative.net/\""
        }
        debug {
            buildConfigField "String", "OMIDPV", "\"$omid_version\""
            buildConfigField "String", "OMIDPN", "\"pubnativenet\""
            buildConfigField "String", "SDK_VERSION", "\"$version_name\""
            buildConfigField "String", "BASE_URL", "\"https://api.pubnative.net/\""
        }
    }
}

dependencies {

    implementation project(':hybid.sdk.core')
    implementation project(':hybid.sdk.banner')
    implementation project(':hybid.sdk.fullscreen')
    implementation project(':hybid.sdk.native')
    implementation project(':hybid.sdk.rewarded')
    implementation project(':hybid.sdk.reporting')
    implementation project(':hybid.sdk.source.pnapi')
    implementation project(':hybid.sdk.viewability')
    implementation project(':hybid.sdk.logger')

    // Remember to update OMSDK version constant found in HyBid.java
    implementation "net.pubnative:hybid.omsdk:$omid_version"

    testImplementation 'com.google.truth:truth:0.36'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'com.google.truth:truth:0.36'
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestImplementation "androidx.test:runner:1.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test:rules:1.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

//--------------------------------------------------------------------------------------------------
// COVERALLS
//--------------------------------------------------------------------------------------------------
apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.kt3k.coveralls"
cobertura.coverageFormats = ['html', 'xml']

//--------------------------------------------------------------------------------------------------
// PUBLISHING
//--------------------------------------------------------------------------------------------------
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.rootProject.files(android.getBootClasspath().join(File.pathSeparator))
    options.memberLevel = JavadocMemberLevel.PUBLIC
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def addDependency(dependency, dependenciesNode) { //finish with dependencies
    if (dependency.group != null && dependency.name != null) {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', dependency.group)
        dependencyNode.appendNode('artifactId', dependency.name)
        dependencyNode.appendNode('version', dependency.version)

        if (dependency.excludeRules.size() > 0) {
            def exclusions = dependencyNode.appendNode('exclusions')
            dependency.excludeRules.each { ExcludeRule ex ->
                def exclusion = exclusions.appendNode('exclusion')
                exclusion.appendNode('groupId', ex.group)
                if (ex.module == null || ex.module.isEmpty()) {
                    exclusion.appendNode('artifactId', "*")
                } else {
                    exclusion.appendNode('artifactId', ex.module)
                }
            }
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId project.group
            artifactId project.name
            version version_name
            artifact javadocJar
            if (branchName == "master") {
                artifact sourcesJar
            }
            android.libraryVariants.all { variant ->
                if (variant.buildType.name == "debug") return
                artifact variant.outputs[0].packageLibrary // This is the aar library
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.withType(ModuleDependency).each {
                    addDependency(it, dependenciesNode)
                }
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}

artifactory {
    contextUrl = System.getenv("ARTIFACTORY_CONTEXT_URL")
    publish {
        repository {
            repoKey = 'verve-gradle-release-local'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
            maven = true
        }

        defaults {
            publishArtifacts = branchName != "master"
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'verve-gradle-release'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
            maven = true
        }
    }
}