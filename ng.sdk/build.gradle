plugins {
    id 'com.android.library'
    id 'com.jfrog.artifactory'
    id 'maven-publish'
}

//==================================================================================================
// PROJECT
//==================================================================================================
def project_name = project.name
def version_name = project.version
if (branchName != null && buildNumber != null && branchName.toString() != "master" && !branchName.toString().contains("/")) {
    version_name = "${version_name}-${branchName}"
    project_name = "${project_name}.beta"
}
version = version_name

//==================================================================================================
// ANDROID
//==================================================================================================

android {
    compileSdk android_api
    defaultConfig {
        minSdkVersion min_android_api
        targetSdkVersion android_api
        versionCode System.getenv("CIRCLE_BUILD_NUM") ? System.getenv("CIRCLE_BUILD_NUM").toInteger() : 1
        versionName version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled System.getenv("CI") ? true : false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'ng.sdk.pro'
            buildConfigField "String", "SDK_VERSION", "\"$version_name\""
            buildConfigField "String", "SDK_WRAPPER_VERSION", "\"$wrapper_version\""
            buildConfigField "String", "DISPLAY_MANAGER_NAME", "\"Smaato\""
            buildConfigField "Boolean", "IS_WRAPPED", "true"
            buildConfigField "String", "BASE_URL", "\"https://api.nextgen.verve.net/\""
            buildConfigField "String", "BASE_RTB_URL", "\"https://dsp.nextgen.verve.net/\""
        }
        debug {
            buildConfigField "String", "SDK_VERSION", "\"$version_name\""
            buildConfigField "String", "SDK_WRAPPER_VERSION", "\"$wrapper_version\""
            buildConfigField "String", "DISPLAY_MANAGER_NAME", "\"Smaato\""
            buildConfigField "Boolean", "IS_WRAPPED", "true"
            buildConfigField "String", "BASE_URL", "\"https://api.nextgen.verve.net/\""
            buildConfigField "String", "BASE_RTB_URL", "\"https://dsp.nextgen.verve.net/\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Explicitly define the source directory where copied files will reside
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
            }
        }
    }

    namespace 'com.verve.ng.sdk'

    /*flavorDimensions "brand"

    productFlavors {
        pubnative {
            dimension "brand"
            namespace 'net.pubnative.lite.sdk'
        }
        nextgen {
            dimension "brand"
            namespace 'com.verve.ng.sdk'
        }
    }

    sourceSets {
        main {

        }
        nextgen {
            java.srcDirs = ['src/nextgen/java']
            res.srcDirs = ['src/nextgen/res']
        }
    }

    defaultConfig {
        namespace 'net.pubnative.lite.sdk'
    }*/
}

dependencies {

    implementation "net.pubnative:hybid.omsdk:$omid_version"
    implementation "androidx.annotation:annotation:1.9.1"
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'org.mockito:mockito-core:5.16.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    androidTestImplementation "androidx.test:runner:1.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test:rules:1.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0-beta01", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

//--------------------------------------------------------------------------------------------------
// COVERALLS
//--------------------------------------------------------------------------------------------------
//cobertura.coverageFormats = ['html', 'xml']

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    def bootClasspathFiles = android.getBootClasspath().findAll { it != null && it.toString().trim() }
    classpath += project.rootProject.files(bootClasspathFiles)
    options.memberLevel = JavadocMemberLevel.PUBLIC
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

static def addDependency(dependency, dependenciesNode) { //finish with dependencies
    if (dependency.group != null && dependency.name != null) {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', dependency.group)
        dependencyNode.appendNode('artifactId', dependency.name)
        dependencyNode.appendNode('version', dependency.version)

        if (dependency.excludeRules.size() > 0) {
            def exclusions = dependencyNode.appendNode('exclusions')
            dependency.excludeRules.each { ExcludeRule ex ->
                def exclusion = exclusions.appendNode('exclusion')
                exclusion.appendNode('groupId', ex.group)
                if (ex.module == null || ex.module.isEmpty()) {
                    exclusion.appendNode('artifactId', "*")
                } else {
                    exclusion.appendNode('artifactId', ex.module)
                }
            }
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId "com.verve"
            artifactId project.name
            version version_name
            artifact javadocJar
            if (branchName.toString() == "master") {
                artifact sourcesJar
            }
            android.libraryVariants.all { variant ->
                if (variant.buildType.name == "debug") return
                artifact variant.outputs[0].packageLibrary // This is the aar library
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.withType(ModuleDependency).each {
                    addDependency(it, dependenciesNode)
                }
            }
        }
    }

    repositories {
        maven {
            name = "Development"
            url = uri("s3://smaato-sdk-releases-internal/")
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
        maven {
            name = "Production"
            url = uri("s3://smaato-sdk-releases/")
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}

tasks.register('correctNgSdkImportStatements') {
    doLast {
        def sourceDir = file("src/main/java")

        if (sourceDir.exists()) {
            fileTree(dir: sourceDir).include("**/*.java").each { File file ->
                def content = file.text
                content = content.replaceAll(/import com\.verve\.ng\.sdk\..*?\.BuildConfig;/, "import com.verve.ng.sdk.BuildConfig;")
                content = content.replaceAll(/import com\.verve\.ng\.sdk\..*?\.R;/, "import com.verve.ng.sdk.R;")
                content = content.replaceAll("net.pubnative.lite.sdk.viewability.Assets.omsdkjs", "com.verve.ng.sdk.viewability.Assets.omsdkjs")
                content = content.replaceAll("net.pubnative.lite.sdk.source.pnapi.BuildConfig.BASE_URL", "com.verve.ng.sdk.BuildConfig.BASE_URL")

                file.text = content
            }
        } else {
            println "Source directory not found: ${sourceDir.path}"
        }
    }
}