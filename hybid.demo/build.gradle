repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "net.pubnative.lite.demo"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode System.getenv("CIRCLE_BUILD_NUM") ? System.getenv("CIRCLE_BUILD_NUM").toInteger() : 1
        versionName project.version
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'lite.sdk.pro'
        }
        debug {
            minifyEnabled false
            useProguard false
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':hybid.sdk')
    implementation project(':hybid.adapters.mopub')
    implementation project(':hybid.adapters.dfp')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation('com.mopub:mopub-sdk:5.3.0@aar') {
        transitive = true
        exclude module: 'libAvid-mopub' // To exclude AVID
        exclude module: 'moat-mobile-app-kit' // To exclude Moat
    }

    implementation 'com.google.android.gms:play-services-ads:11.8.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
}

if (System.getenv("CIRCLECI")) {

    //----------------------------------------------------------------------------------------------
    // FABRIC
    //----------------------------------------------------------------------------------------------
    apply plugin: 'io.fabric'
    println ""
    println "======== FABRIC ========"
    try {
        File file = new File("${project.projectDir.absolutePath}/fabric.properties")
        if (!file.canRead()) {
            file.createNewFile()
            file << "apiKey=${System.getenv("FABRIC_API_KEY")}\n"
            file << "apiSecret=${System.getenv("FABRIC_API_SECRET")}\n"
        }
        println " - Success"
        println "========================"
        println ""
    } catch (IOException ioException) {
        println " - Error: "
        println " ${ioException.toString()}"
        println "========================"
        println ""
    }
}
apply plugin: 'kotlin-android-extensions'
